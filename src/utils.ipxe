#!ipxe
set sigs_enabled true
menu Utilities
item --gap Utilities:
item avg ${space} AVG Rescue CD
item clonezilla ${space} Clonezilla
item dban ${space} DBAN
item gparted ${space} GParted
item grml ${space} Grml
item hdt ${space} Hardware Detection Tool
item memtest_420 ${space} Memtest86+ 4.20
item memtest_501 ${space} Memtest86+ 5.01
item netbootcd ${space} NetbootCD.us
item partition_wizard ${space} Partition Wizard
item pogostick ${space} Pogostick - Offline Windows Password and Registry Editor
item supergrub ${space} Super Grub2 Disk
item --gap netboot.xyz tools:
item nbxyz-custom ${space} Set Github User [user: ${github_user}]
item testdistro ${space} Test Distribution ISO
item testpr ${space} Test netboot.xyz branch
choose --default ${menu} menu || goto utils_exit
echo ${cls}
goto ${menu} ||
chain ${menu}.ipxe || goto utils_exit
goto utils_exit

:avg
set util_path download.avg.com/filedir/inst/avg_arl_cdi_all_120_150814a10442.iso
set util_file avg_arl_cdi_all_120_150814a10442.iso
goto boot_memdisk

:clonezilla
set util_path sourceforge.net/projects/clonezilla/files/clonezilla_live_stable/2.4.5-20/clonezilla-live-2.4.5-20-amd64.iso/download
set util_file clonezilla-live-2.4.5-20-amd64.iso
goto boot_memdisk

:dban
set util_path sourceforge.net/projects/dban/files/dban/dban-2.3.0/dban-2.3.0_i586.iso/download
set util_file dban-2.3.0_i586.iso
goto boot_memdisk

:gparted
set util_path sourceforge.net/projects/gparted/files/gparted-live-stable/0.25.0-1/gparted-live-0.25.0-1-amd64.iso/download
set util_file gparted-live-0.25.0-1-amd64.iso
goto boot_memdisk

:grml
set util_path mirror.rit.edu/grml/grml64-full_2014.03.iso
set util_file grml64-full_2014.03.iso
goto boot_memdisk

:hdt
set util_path www.hdt-project.org/raw-attachment/wiki/hdt-0.5.0/hdt-0.5.2.iso
set util_file hdt-0.5.2.iso
goto boot_memdisk

:memtest_420
set util_path boot.netboot.xyz/utils/memtest86-4.20.bin
set util_file memtest86-4.20.bin
goto boot_memtest

:memtest_501
set util_path boot.netboot.xyz/utils/memtest86-5.01.bin
set util_file memtest86-5.01.bin
goto boot_memtest

:partition_wizard
set util_path www.partitionwizard.com/download/pwfree91-x64.iso
set util_file pwfree91-x64.iso
goto boot_memdisk

:pogostick
set util_path pogostick.net/~pnh/ntpasswd/cd140201.zip
set util_file cd140201.zip
goto boot_memdisk

:supergrub
set util_path forja.cenatic.es/frs/download.php/file/1844/super_grub2_disk_hybrid_2.02s3.iso
set util_file super_grub2_disk_hybrid_2.02s3.iso
goto boot_memdisk

:boot_memdisk
iseq ${sigs_enabled} true && goto boot_memdisk_verify ||
kernel ${memdisk} iso raw
initrd http://${util_path}
boot
goto utils_exit

:boot_memdisk_verify
imgverify https://${boot_domain}/memdisk ${sigs}/memdisk.sig || goto error
kernel memdisk iso raw
imgverify http://${util_path} ${external_sigs}${util_path}.sig || goto error
initrd ${util_file}
boot
goto utils_exit

:boot_memtest
chain https://${util_kernel} && goto main_menu ||
goto utils_exit

:nbxyz-custom
echo EXPERIMENTAL
echo 
echo Make sure you have a fork of https://github.com/antonym/netboot.xyz-custom.
echo You can then customize your fork as needed and set up your own custom options.
echo Once your username is set, a custom option will appear on the main menu.
echo
echo -n Please enter your Github username: ${} && read github_user
goto utils_exit 

:netbootcd
kernel http://netbootcd.us/downloads/6.4.1/vmlinuz
initrd http://netbootcd.us/downloads/6.4.1/nbinit4.gz
boot
goto utils_exit

:testdistro
echo This option will allow you to test booting an ISO using memdisk. Please
echo specify the url of the ISO you want to test and it will automatically
echo attempt to load the ISO using memdisk.
echo -n URL: ${} && read distro_iso
kernel ${memdisk} iso raw
initrd ${distro_iso}
boot
goto utils_exit

:testpr
clear github_user
clear github_hash
echo This will chainload into a testing branch of netboot.xyz.  You'll need to enter
echo your Github User and the first part of the commit hash of the commit you want
echo to test or the branch name.  This assumes you are testing from a netboot.xyz 
echo repo.
echo
echo -n Specify Github User Name: ${} && read github_user
echo -n Specify branch name or hash ( i.e. my_feature or 30b7ca ): ${} && read github_hash
echo
echo Attempting to chainload:
chain --autofree https://raw.githubusercontent.com/${github_user}/netboot.xyz/${github_hash}/src/menu.ipxe || echo Unable to find branch... && sleep 5 && goto utils_exit
goto utils_exit

:utils_exit
clear menu
exit 0
